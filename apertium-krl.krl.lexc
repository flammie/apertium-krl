!! Karelian lexc
!! GNU GPLv3,
!! Authors: Tommi A Pirinen etc. (see AUTHORS)

Multichar_Symbols
!! Apertium standard tags:
%<abbr%>
%<abe%>
%<abl%>
%<acc%>
%<acr%>
%<actv%>
%<ade%>
%<adj%>
%<adv%>
%<agent%>
%<al%>
%<all%>
%<ant%>
%<card%>
%<cmp%>
%<cmp-split%>
%<cnjadv%>
%<cnjcoo%>
%<cnjsub%>
%<cog%>
%<com%>
%<compound-R%>
%<compound-only-L%>
%<cond%>
%<conneg%>
%<def%>
%<dem%>
%<det%>
%<ela%>
%<enc%>
%<ess%>
%<f%>
%<gen%>
%<ij%>
%<ill%>
%<imp%>
%<impers%>
%<ind%>
%<ine%>
%<infa%>
%<infe%>
%<infma%>
%<infminen%>
%<ins%>
%<itg%>
%<lat%>
%<loc%>
%<m%>
%<mf%>
%<n%>
%<neg%>
%<nom%>
%<np%>
%<num%>
%<ord%>
%<p1%>
%<p2%>
%<p3%>
%<par%>
%<part%>
%<past%>
%<pasv%>
%<pers%>
%<pl%>
%<pneg%>
%<pos%>
%<post%>
%<pot%>
%<pp%>
%<pprs%>
%<pri%>
%<prn%>
%<punct%>
%<pxpl1%>
%<pxpl2%>
%<pxsg1%>
%<pxsg2%>
%<pxsp3%>
%<qst%>
%<qu%>
%<rec%>
%<reflex%>
%<rel%>
%<sg%>
%<sp%>
%<sup%>
%<sym%>
%<top%>
%<tra%>
%<use_archaic%>
%<use_blacklist%>
%<use_dialect%>
%<use_foreign%>
%<use_nonstd%>
%<vaux%>
%<vblex%>


{-}

LEXICON Root
!! LEXICONS per class
    0   NOUN ;
    0   ADJ ;
    0   VERB    ;
    0   NUM ;
    0   DIGITS ;
    0   PRON    ;
    0   ADP    ;
    0   ADV    ;
    0   INTJ ;
    0   PUNCT ;
    0   SYM ;
    0   CCONJ ;
    0   SCONJ ;
    0   CCONJ|VERB ;
    0   PROPN ;
    0   ACRONYM ;
    0   ABBREVIATION    ;
    0   AUX    ;
    0   DET    ;
    0   X    ;
0	SUFFIX	;


LEXICON ADJ

oma%<adj%>:om ADJ_OMA ;
kielelline%<adj%>:kielelli ADJ_KIELI/NE ;
livvin#kieline%<adj%>:livvin0kieli ADJ_KIELI/NE ;
enetsi#kieline%<adj%>:enetsi0kieli ADJ_KIELI/NE ;
hantin#kieline%<adj%>:hantin0kieli ADJ_KIELI/NE ;
pohjone%<adj%>:pohjo ADJ_KIELI/NE ;
suomelane%<adj%>:suomela ADJ_KIELI/NE ;

LEXICON ADP

# ;

LEXICON ADV

# ;

LEXICON AUX

olla%<vaux%>:o AUX_O/LLA ;
ei%<vaux%>:e AUX_E/I ;

LEXICON CCONJ

# ;

LEXICON CCONJ|VERB

# ;

LEXICON DIGITS

%0%<num%>:%0 # ;
1%<num%>:1 # ;
2%<num%>:2 # ;
3%<num%>:3 # ;
4%<num%>:4 # ;
5%<num%>:5 # ;
6%<num%>:6 # ;
7%<num%>:7 # ;
8%<num%>:8 # ;
9%<num%>:9 # ;

LEXICON DET

# ;

LEXICON INTJ

# ;

LEXICON NOUN

lapsi%<n%>:laps NOUN_LAPS/I ;
vähembistökieli%<n%>:vähembistökiel NOUN_LAPS/I ;
ruado%<n%>:rua NOUN_RUA/DO ;
elavuttamine%<n%>:elavuttami NOUN_ELAVUTTAMI/NE ;
karjalane%<n%>:karjala NOUN_ELAVUTTAMI/NE ;
naine%<n%>:nai NOUN_ELAVUTTAMI/NE ;
koti%<n%>:ko NOUN_KO/TI ;
päivy#koti%<n%>:päivy0ko NOUN_KO/TI ;
pohjone%<n%>:pohjo NOUN_ELAVUTTAMI/NE ;
kieli#pezä%<n%>:kieli0pez NOUN_PEZ/Ä ;
mua%<n%>:mu NOUN_MU/A ;
niemi#mua%<n%>:niemi0mu NOUN_MU/A ;


LEXICON NUM

yksi%<num%>:y NUM_Y/KSI ;
kaksi%<num%>:ka NUM_KA/KSI ;
kolme%<num%>:kolm NUM_KOLM/E ;
n’el’l’ä%<num%>:n’el’ NUM_NEL/LÄ ;
viizi%<num%>:vii  NUM_VII/ZI ;
kuuzi%<num%>:kuu  NUM_KUU/ZI ;
seiččemen%<num%>:seičče  NUM_SEIČČE/MEN ;
kaheksan%<num%>:kaheksa  NUM_KAHEKSA/N ;
yheksän%<num%>:yheksä    NUM_YHEKSÄ/N ;
kymmenen%<num%>:kymmene  NUM_KYMMENE/N ;
sada%<num%>:sada       NUM_MILJONA ;
miljona%<num%>:miljona NUM_MILJONA ;


LEXICON PRON

minä%<prn%>%<use%_dialect%>%<pers%>%<p1%>:mi PRON_MI/NÄ ;
mie%<prn%>%<pers%>%<p1%>%<sg%>:mi PRON_MI/E ;
sie%<prn%>%<pers%>%<p2%>%<sg%>:si PRON_MI/E ;
iče%<prn%>%<refl%>:iče   PRON_IČE ;

LEXICON PROPN

Petroskoi%<np%>%<top%>:Petroskoi PROPN_PETROSKOI ;
Karjala%<np%>%<top%>:Karjala PROPN_PETROSKOI ;

LEXICON PUNCT

%!%<punct%>:%!	PUNCT_NONSTD_EXCL_LOOP	;
?%<punct%>:?	PUNCT_NONSTD_EXCL_LOOP	;
,%<punct%>:,	PUNCT_PISTE	;
%;%<punct%>:%;	PUNCT_PISTE	;
%:%<punct%>:%:	PUNCT_PISTE	;
--%<punct%>:--	PUNCT_PISTE	;
-%<punct%>:-	PUNCT_PISTE	;
–%<punct%>:–	PUNCT_PISTE	;
—%<punct%>:—	PUNCT_PISTE	;
˗%<punct%>:˗	PUNCT_PISTE	;
%>%<punct%>:%>	PUNCT_PISTE	;
)%<punct%>:)	PUNCT_PISTE	;
]%<punct%>:]	PUNCT_PISTE	;
}%<punct%>:}	PUNCT_PISTE	;
›%<punct%>:›	PUNCT_PISTE	;
’%<punct%>:’	PUNCT_PISTE	;
%"%<punct%>:%"	PUNCT_PISTE	;
”%<punct%>:”	PUNCT_PISTE	;
»%<punct%>:»	PUNCT_PISTE	;
%<%<punct%>:%<	PUNCT_PISTE	;
(%<punct%>:(	PUNCT_PISTE	;
[%<punct%>:[	PUNCT_PISTE	;
{%<punct%>:{	PUNCT_PISTE	;
‹%<punct%>:‹	PUNCT_PISTE	;
¡%<punct%>:¡	PUNCT_PISTE	;
¿%<punct%>:¿	PUNCT_PISTE	;
―%<punct%>:―	PUNCT_PISTE	;
...%<punct%>:...	PUNCT_PISTE	;
.%<punct%>:.	PUNCT_PISTE	;
(%!)%<punct%>:(%!)	PUNCT_PISTE	;
(?)%<punct%>:(?)	PUNCT_PISTE	;
…%<punct%>:…	PUNCT_PISTE	;
‥%<punct%>:‥	PUNCT_PISTE	;
'%<punct%>:'	PUNCT_PISTE	;
«%<punct%>:«	PUNCT_PISTE	;
“%<punct%>:“	PUNCT_PISTE	;
....%<punct%>:....	PUNCT_PISTE	;
..%<punct%>:..	PUNCT_PISTE	;
-%<punct%>:-	PUNCT_NONSTD_DASH_LOOP	;
’%<punct%>:’	PUNCT_PISTE	;
%"%<punct%>:%"	PUNCT_PISTE	;
”%<punct%>:”	PUNCT_PISTE	;
»%<punct%>:»	PUNCT_PISTE	;
/%<punct%>%<n%>:/	PUNCT_PISTE	;
*%<punct%>:*	PUNCT_PISTE	;
%!%<punct%>:%!	PUNCT_PISTE	;
?%<punct%>:?	PUNCT_PISTE	;
´%<punct%>:´	PUNCT_PISTE	;
`%<punct%>:`	PUNCT_PISTE	;
'%<punct%>:'	PUNCT_PISTE	;
«%<punct%>:«	PUNCT_PISTE	;

LEXICON SCONJ

# ;

LEXICON SYM

¼%<sym%>%<n%>:¼	SYM_.	;
½%<sym%>%<n%>:½	SYM_.	;
¹%<sym%>%<n%>:¹	SYM_.	;
€%<sym%>:€	SYM_.	;
×%<sym%>:×	SYM_.	;
·%<sym%>:·	SYM_.	;
®%<sym%>:®	SYM_.	;
™%<sym%>:™	SYM_.	;
@%<sym%>:@	SYM_.	;
¤%<sym%>:¤	SYM_.	;
¢%<sym%>:¢	SYM_.	;
*%<sym%>:*	SYM_.	;
#%<sym%>%<n%>:#	SYM_.	;
$%<sym%>:$	SYM_.	;
£%<sym%>:£	SYM_.	;
₤%<sym%>:₤	SYM_.	;
|%<sym%>:|	SYM_.	;
¶%<sym%>:¶	SYM_.	;
%%%<sym%>%<n%>:%%	SYM_.	;
+%<sym%>:+	SYM_.	;
♥%<sym%>:♥	SYM_.	;
→%<sym%>:→	SYM_.	;
^%<sym%>:^	SYM_.	;
≤%<sym%>:≤	SYM_.	;
=%<sym%>%<n%>:=	SYM_.	;
≥%<sym%>%<n%>:≥	SYM_.	;
µ%<sym%>%<n%>:µ	SYM_.	;
%;-)%<sym%>:%;-)	SYM_.	;
%;)%<sym%>:%;)	SYM_.	;
%:-)%<sym%>:%:-)	SYM_.	;
%:-#%<sym%>:%:-#	SYM_.	;
%:)%<sym%>:%:)	SYM_.	;
%:#%<sym%>:%:#	SYM_.	;
‘%<sym%>:‘	SYM_.	;
§%<sym%>:§	SYM_.	;
♪%<sym%>:♪	SYM_.	;
☹%<sym%>:☹	SYM_.	;
☺%<sym%>:☺	SYM_.	;
☻%<sym%>:☻	SYM_.	;

´%<sym%>:´	SYM_.	;
`%<sym%>:`	SYM_.	;
±%<sym%>:±	SYM_.	;
−%<sym%>:−	SYM_.	;
~%<sym%>:~	SYM_.	;
_%<sym%>:_	SYM_.	;
\%<sym%>:\	SYM_.	;
‰%<sym%>:‰	SYM_.	;
¥%<sym%>:¥	SYM_.	;
°%<sym%>:°	SYM_.	;
©%<sym%>:©	SYM_.	;
§§%<sym%>:§§	SYM_.	;
%%%%%<sym%>:%%%%	SYM_.	;
′%<sym%>:′	SYM_.	;
²%<sym%>%<n%>:²	SYM_.	;
€%<sym%>:€	SYM_.	;
¥%<sym%>:¥	SYM_.	;
,%<sym%>:,	SYM_.	;
#%<sym%>%<n%>:#	SYM_.	;
$%<sym%>:$	SYM_.	;
%<3%<sym%>:%<3	SYM_.	;
³%<sym%>%<n%>:³	SYM_.	;
%:-D%<sym%>:%:-D	SYM_.	;
%:D%<sym%>:%:D	SYM_.	;
m€%<sym%>:m€	SYM_.	;
M€%<sym%>:M€	SYM_.	;
%:-P%<sym%>:%:-P	SYM_.	;
%:P%<sym%>:%:P	SYM_.	;
%:-x%<sym%>:%:-x	SYM_.	;
%:x%<sym%>:%:x	SYM_.	;
%:-X%<sym%>:%:-X	SYM_.	;
%:X%<sym%>:%:X	SYM_.	;
 %<sym%>: 	SYM_.	;

LEXICON VERB

ozuttoa%<vblex%>:ozut VERB_OZUT/TOA ;

LEXICON X

# ;

LEXICON SUFFIX

# ;

LEXICON ABBREVIATION

# ;

LEXICON ACRONYM

# ;

!! Puncts etc.

LEXICON PUNCT_NONSTD_DASH_LOOP

:- PUNCT_NONSTD_DASH_LOOP ;
:- # ;

LEXICON PUNCT_NONSTD_EXCL_LOOP

:%! PUNCT_NONSTD_EXCL_LOOP ;
:%! # ;

!! Stems

LEXICON AUX_O/LLA

0:llu   AUX_PCP ;
0:lla   AUX_PASSIVE_H ;
0:le    AUX_CONNEG_0 ;
0:di    AUX_PAST_STRONG ;
0:n     AUX_3SG_0 ;

LEXICON AUX_E/I

0:i    AUX_3SG_0 ;


LEXICON VERB_OZUT/TOA

0:etti VERB_PASSIVE_H ;

LEXICON ADJ_OMA

0:i ADJ_CASES_PL ;
0:a ADJ_CASES_SG ;

LEXICON ADJ_KIELI/NE

0:ne ADJ_SGNOM_0 ;
0:ze ADJ_CASES_SG ;
0:s  ADJ_SGPAR_TY ;

LEXICON PROPN_PETROSKOI

0 PROPN_CASES ;

LEXICON PRON_MI/NÄ

0:nä  PRON_SGNOM_0 ;
0:nu  PRON_CASES_SG ;

LEXICON PRON_MI/E

0:e PRON_SGNOM_0 ;
0:u PRON_CASES_SG ;

LEXICON PRON_IČE

0  PRON_SGNOM_0 ;
0  PRON_CASES_SG ;

LEXICON NOUN_LAPS/I

0:i NOUN_CASES_SG ;
0   NOUN_CASES_PL ;

LEXICON NOUN_RUA/DO

0:to NOUN_CASES_SG ;
0:do NOUN_CASES_SG ;

LEXICON NOUN_KO/TI

0:ti NOUN_CASES_SG_STRONG ;
0:i  NOUN_CASES_SG_WEAK ;

LEXICON NOUN_ELAVUTTAMI/NE

0:ne NOUN_SGNOM_0 ;
0:ze NOUN_CASES_SG ;
0:s  NOUN_SGPAR_TU ;
0:z  NOUN_CASES_PL ;

LEXICON NOUN_PEZ/Ä

0:ä  NOUN_SGNOM_0 ;
0    NOUN_CASES_PL ;

LEXICON NOUN_MU/A

0:a NOUN_SGNOM_0 ;
0:a NOUN_CASES_SG ;

LEXICON NUM_Y/KSI

0:ksi NUM_SGNOM_0 ;
0:hte NUM_CASES_SG ;

LEXICON NUM_KA/KSI

0:ksi NUM_SGNOM_0 ;
0:hte NUM_CASES_SG ;

LEXICON NUM_KOLM/E

0:e NUM_SGNOM_0 ;
0:e NUM_CASES_SG ;

LEXICON NUM_NEL/LÄ

0:l’ä  NUM_SGNOM_0 ;
0:l’ä  NUM_CASES_SG ;

LEXICON NUM_VII/ZI

0:zi  NUM_SGNOM_0 ;
0:jje NUM_CASES_SG ;

LEXICON NUM_KUU/ZI

0:zi  NUM_SGNOM_0 ;
0:vve NUM_CASES_SG ;

LEXICON NUM_SEIČČE/MEN

0:men NUM_SGNOM_0 ;
0:me  NUM_CASES_SG ;

LEXICON NUM_KAHEKSA/N

0:n  NUM_SGNOM_0 ;
0    NUM_CASES_SG ;

LEXICON NUM_YHEKSÄ/N

0:n  NUM_SGNOM_0 ;
0    NUM_CASES_SG ;

LEXICON NUM_KYMMENE/N

0:n  NUM_SGNOM_0 ;
0   NUM_CASES_SG ;

LEXICON NUM_MILJONA

0 NUM_SGNOM_0 ;
0 NUM_CASES_SG ;

LEXICON PUNCT_PISTE

0   # ;

LEXICON SYM_.

0 # ;

!! Morphs
LEXICON AUX_PCP

%<pcpnut%>:t # ;

LEXICON AUX_3SG_0

%<act%>%<pri%>%<p3%>%<sg%>:0 # ;

LEXICON AUX_CONNEG_0

%<conneg%>%<sg%>:0 # ;

LEXICON AUX_PAST_STRONG

%<actv%>%<past%>%<act%>%<p3%>:h # ;

LEXICON AUX_PASSIVE_H

%<actv%>%<pri%>%<p3%>%<pl%>:h # ;
%<pasv%>:h # ;

LEXICON VERB_PASSIVE_H

%<actv%>%<pri%>%<p3%>%<pl%>:h # ;
%<pasv%>:h # ;

LEXICON PROPN_CASES

%<sg%>%<gen%>:n # ;
%<sg%>%<ill%>:h # ;

LEXICON ADJ_SGNOM_0

%<sg%>%<nom%>:0 # ;

LEXICON ADJ_SGPAR_TY

%<sg%>%<par%>:ty  # ;

LEXICON ADJ_CASES_SG

%<sg%>%<ine%>:s # ;
%<sg%>%<gen%>:n # ;
%<pl%>%<nom%>:t # ;

LEXICON ADJ_CASES_PL

%<pl%>%<par%>:i # ;

LEXICON NOUN_CASES_SG

0 NOUN_CASES_SG_WEAK ;
0 NOUN_CASES_SG_STRONG ;

LEXICON NOUN_CASES_SG_STRONG

%<sg%>%<nom%>:0 # ;

LEXICON NOUN_CASES_SG_WEAK

%<sg%>%<ine%>:s # ;
%<sg%>%<gen%>:n # ;

LEXICON NOUN_CASES_PL

%<pl%>%<gen%>:ien # ;
%<pl%>%<all%>:ile # ;

LEXICON NOUN_SGNOM_0

%<sg%>%<nom%>:0 # ;

LEXICON NOUN_SGPAR_TU

%<sg%>%<par%>:tu # ;

LEXICON NUM_SGNOM_0

%<sg%>%<nom%>:0 # ;


LEXICON NUM_CASES_SG

%<sg%>%<gen%>:n # ;
%<sg%>%<ill%>:h # ;
%<pl%>%<nom%>:t # ;

LEXICON PRON_SGNOM_0

%<sg%>%<nom%>:0 # ;

LEXICON PRON_CASES_SG

%<sg%>%<gen%>:n # ;
%<sg%>%<ine%>:s # ;
%<sg%>%<ine%>:šša # ;
%<sg%>%<ill%>:h # ;



! vim: set ft=xfst-lexc:
